#!/usr/bin/perl
# Displays the DIMM and CPU Core temperatures of my Xeon workstation, one row
# of text per minute.
#
# Copyright (c) 2013, Tommi Rantala <tt.rantala@gmail.com>
# All rights reserved.
#
# Redistribution and use in source and binary forms, with or without
# modification, are permitted provided that the following conditions are met:
#
# 1. Redistributions of source code must retain the above copyright notice,
# this list of conditions and the following disclaimer.
#
# 2. Redistributions in binary form must reproduce the above copyright notice,
# this list of conditions and the following disclaimer in the documentation
# and/or other materials provided with the distribution.
#
# THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
# AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
# IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
# ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
# LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
# CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
# SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
# INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
# CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
# ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
# POSSIBILITY OF SUCH DAMAGE.

use v5.10;
use strict;
use Data::Dumper;
use List::Util qw/min max/;
use Term::ANSIColor qw/:constants/;

printf "%s:", scalar localtime();

foreach (glob '/sys/devices/platform/i5k_amb.0/temp*_label') {
	open my $fh, $_;
	my $label = <$fh> // '';
	chomp $label;

	if ($label =~ /^Ch\. (\d) DIMM (\d)/) {
		$label = 'DIMM ' . (1 + $2 + 2*$1);
	}

	printf "|%6s", $label;
}
printf "|  ";

my $i = 1;
foreach (glob '/sys/devices/platform/coretemp.*/temp*_label') {
	open my $fh, $_;
	my $label = <$fh> // '';
	chomp $label;

	if ($label =~ /^Core (\d)/) {
		$label = "Core $i";
	}

	printf "|%6s", $label;
	$i++;
}
say '|';

my @dimmtemp_files = glob '/sys/devices/platform/i5k_amb.0/temp*_input';
my @coretemp_files = glob '/sys/devices/platform/coretemp.*/temp*_input';
#print Dumper \@dimmtemp_files;
#print Dumper \@coretemp_files;

sub temps {
	my @temps;
	foreach (@_) {
		open my $fh, $_;
		my $temp = <$fh>;
		if (defined $temp) {
			chomp $temp;
			$temp /= 1000;
		}
		push @temps, $temp;
	}
	return @temps;
}

sub dimmtemps { temps(@dimmtemp_files) }
sub coretemps { temps(@coretemp_files) }

sub print_temps {
	foreach my $temp (@_) {
		if (defined $temp && $temp == max(@_)) {
			print RED;
		} elsif (defined $temp && $temp == min(grep {defined} @_)) {
			print GREEN;
		}
		printf "|%5.2fC" . RESET, $temp;
	}
}

while (1) {
	my @dimmtemps = dimmtemps();
	my @coretemps = coretemps();

	printf "%s:", scalar localtime();
	print_temps(@dimmtemps);
	print '|  ';
	print_temps(@coretemps);
	say '|';

	sleep 60;
}
